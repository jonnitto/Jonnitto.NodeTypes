prototype(NT:Content) < prototype(TS:Tag) {
	@context {
		property = ${this.property}
		config = ${this.config}
		inlineTag = ${this.inlineTag}
		nodeTypeName = NT:NodeTypeName
		isContentElement = ${this.isContentElement}
		containerFluid = ${this.containerFluid}
		isOnDocument = ${this.isOnDocument}
		isFirstElement = ${this.isFirstElement}
	}

	property = NULL
	tagName = 'div'
	isContentElement = false
	containerFluid = false
	isFirstElement = NT:IsFirstElement
	isFirstElement.@if.isContentElement = ${this.isContentElement}

	isOnDocument = ${q(node).parent().parent().is(this.config.instanceOfDocument)}
	config = ${Configuration.setting('Jonnitto.NodeTypes')}

	attributes.class = TS:RawArray {
		nodeType = ${isContentElement ? nodeTypeName : false}
		# Is parent a document => add container class
		container = TS:Value {
			value = ${containerFluid ? config.containerFluid : config.container}
			@if.isOnDocumentAndContentElement = ${isOnDocument && isContentElement}
		}
	}

	content = ''

	@process.contentElementWrapping = TS:Case {
		isContentElement {
			condition = ${isContentElement}
			renderer = ContentElementWrapping
		}
	}
	@if.render = NT:NeededProperties
}
