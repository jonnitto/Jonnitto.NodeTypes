prototype(NT:Content) < prototype(Fusion:Tag) {
	@context {
		property = ${this.property}
		inlineTag = ${this.inlineTag}
		nodeTypeName = NT:NodeTypeName
		isContentElement = ${this.isContentElement}
		containerFluid = ${this.containerFluid}
		isOnDocument = ${this.isOnDocument}
		isFirstElement = ${this.isFirstElement}
	}

	property = NULL
	tagName = 'div'
	isContentElement = false
	containerFluid = false
	isFirstElement = NT:IsFirstElement
	isFirstElement.@if.isContentElement = ${this.isContentElement}

	isOnDocument = ${q(node).parent().parent().is(configJonnittoNodeTypes.instanceOfDocument)}

	attributes.class = Fusion:RawArray {
		nodeType = ${isContentElement ? nodeTypeName : false}
		# Is parent a document => add container class
		container = Fusion:Value {
			value = ${containerFluid ? configJonnittoNodeTypes.containerFluid : configJonnittoNodeTypes.container}
			@if.isOnDocumentAndContentElement = ${isOnDocument && isContentElement}
		}
	}

	content = ''

	@process.contentElementWrapping = ContentElementWrapping
	@if.render = NT:NeededProperties
}
