prototype(NT:Content) < prototype(Fusion:Tag) {
	@context {
		nodeTypeName = NT:NodeTypeName
		isContentElement = ${this.isContentElement}
		containerFluid = ${this.containerFluid}
		isOnDocument = ${this.isOnDocument}
		isFirstElement = ${this.isFirstElement}
	}

	tagName = 'div'
	isContentElement = false
	containerFluid = false
	isFirstElement = NT:IsFirstElement
	isFirstElement.@if.isContentElement = ${this.isContentElement}

	isOnDocument = ${q(node).parent().parent().is(Configuration.setting('Jonnitto.NodeTypes.instanceOfDocument'))}

	attributes.class = Fusion:RawArray {
		nodeType = ${isContentElement ? nodeTypeName : false}
		# Is parent a document => add container class
		container = Fusion:Value {
			value = ${containerFluid ? Configuration.setting('Jonnitto.NodeTypes.containerFluid') : Configuration.setting('Jonnitto.NodeTypes.container')}
			@if.isOnDocumentAndContentElement = ${isOnDocument && isContentElement}
		}
	}

	content = ''

	@process.contentElementWrapping = ContentElementWrapping
	@if.render = NT:NeededProperties
}
