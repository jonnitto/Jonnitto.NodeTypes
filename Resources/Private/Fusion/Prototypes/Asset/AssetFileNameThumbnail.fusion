prototype(NT:AssetFileNameThumbnail) < prototype(Fusion:Array) {
	headline = Fusion:Tag {
		tagName = ${Configuration.setting('Jonnitto.NodeTypes.assetlist.thumbnail.headlineTag')}
		attributes.class = Fusion:RawArray {
			text = 'asset-headline'
		}
		content = Fusion:Case {
			hasTitle {
				condition = ${asset.title}
				renderer = ${asset.title}
			}
			default {
				condition = true
				renderer = ${String.replace(String.replace(asset.resource.filename, '_', ' '), '.' + asset.resource.fileextension, '')}
			}
		}
	}

	thumbnail = Fusion:Tag {
		tagName = 'figure'
		attributes.class = 'asset-thumbnail'
		content = NT:Plainimage {
			asset = ${asset}
			maximumWidth = ${Configuration.setting('Jonnitto.NodeTypes.assetlist.thumbnail.width')}
			checkRatio = false
			additionalClass = 'img-asset-thumbnail'
		}
	}

	text = Fusion:Tag {
		tagName = 'div'
		attributes.class = 'asset-text'
		content = Fusion:Array {
			download = ${Translation.translate(forceDownload ? 'file.download' : 'file.open', null, [String.toUpperCase(asset.resource.fileextension)], null, 'Jonnitto.NodeTypes')}
			caption = ${' ' + asset.caption + ' '}
			info = Fusion:Tag {
				tagName = 'small'
				attributes.class = 'asset-text-info'
				content = NT:Filesize
			}
		}
	}

	@if.hasAsset = ${asset}
}
